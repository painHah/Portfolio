
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОборудованиеОрганизации.Очистить();
	Движения.ОборудованиеОрганизации.Записать();
	Движения.ОборудованиеОрганизации.Записывать = Истина;
	
	СостояниеНаСкладе = Перечисления.СостояниеОборудования.НаСкладе;
	СостояниеВЭксплуатации = Перечисления.СостояниеОборудования.ВЭксплуатации;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВводВЭксплуатациюПереченьОборудования.Оборудование КАК Оборудование,
	|	СУММА(ВводВЭксплуатациюПереченьОборудования.Количество) КАК Количество
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.ВводВЭксплуатацию.ПереченьОборудования КАК ВводВЭксплуатациюПереченьОборудования
	|ГДЕ
	|	ВводВЭксплуатациюПереченьОборудования.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВводВЭксплуатациюПереченьОборудования.Оборудование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОборудованиеОрганизацииОстатки.Оборудование КАК Оборудование,
	|	ОборудованиеОрганизацииОстатки.ГоденДо КАК ГоденДо,
	|	ОборудованиеОрганизацииОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	ОборудованиеОрганизацииОстатки.СуммаОстаток КАК СуммаОстаток
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрНакопления.ОборудованиеОрганизации.Остатки(
	|			&Дата,
	|			СостояниеОборудования = &СостояниеНаСкладе
	|				И Оборудование В
	|					(ВЫБРАТЬ
	|						ДанныеДокумента.Оборудование КАК Оборудование
	|					ИЗ
	|						ДанныеДокумента КАК ДанныеДокумента)
	|				И ГоденДо > &Дата) КАК ОборудованиеОрганизацииОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Оборудование КАК Оборудование,
	|	ДанныеДокумента.Оборудование.Наименование КАК ОборудованиеНаименование,
	|	ДанныеДокумента.Оборудование.СрокЭксплуатации КАК ОборудованиеСрокЭксплуатации,
	|	ДанныеДокумента.Количество КАК Количество,
	|	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(Остатки.СуммаОстаток, 0) КАК СуммаОстаток,
	|	ЕСТЬNULL(Остатки.ГоденДо, ДАТАВРЕМЯ(1, 1, 1)) КАК ГоденДо,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Остатки.КоличествоОстаток, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(Остатки.СуммаОстаток, 0) / Остатки.КоличествоОстаток
	|	КОНЕЦ КАК СебестоимостьЕдиницы
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
	|		ПО ДанныеДокумента.Оборудование = Остатки.Оборудование
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоденДо
	|ИТОГИ
	|	СУММА (Количество),
	|	СУММА (КоличествоОстаток),
	|	МАКСИМУМ (ОборудованиеНаименование)
	|ПО
	|	Оборудование";
	
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("СостояниеНаСкладе", СостояниеНаСкладе);
	
	ВыборкаПоОборудованию = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоОборудованию.Следующий() Цикл
		Если ВыборкаПоОборудованию.Количество > ВыборкаПоОборудованию.КоличествоОстаток Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно оборудования %1, остаток на складе: %2",
			ВыборкаПоОборудованию.ОборудованиеНаименование, ВыборкаПоОборудованию.КоличествоОстаток);
			Сообщение.Сообщить();
			Отказ = Истина;
			Продолжить;	
		КонецЕсли; 
		ВыборкаПоСрокамГодности = ВыборкаПоОборудованию.Выбрать();
		ОсталосьСписать = ВыборкаПоОборудованию.Количество;
		
		Пока
			ВыборкаПоСрокамГодности.Следующий() И ОсталосьСписать > 0 Цикл
			Списываем = Мин(ОсталосьСписать, ВыборкаПоСрокамГодности.КоличествоОстаток);
			СуммаСписания = ВыборкаПоСрокамГодности.СуммаОстаток;
			Если Списываем <> ВыборкаПоСрокамГодности.КоличествоОстаток Тогда
				СуммаСписания = Списываем * ВыборкаПоСрокамГодности.СебестоимостьЕдиницы;	
			КонецЕсли;
			
			Движение = Движения.ОборудованиеОрганизации.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.СостояниеОборудования = СостояниеНаСкладе;
			Движение.Оборудование = ВыборкаПоСрокамГодности.Оборудование;
			Движение.ГоденДо = ВыборкаПоСрокамГодности.ГоденДо;
			Движение.Количество = Списываем;
			Движение.Сумма = СуммаСписания;
			
			Движение = Движения.ОборудованиеОрганизации.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.СостояниеОборудования = СостояниеВЭксплуатации;
			Движение.Оборудование = ВыборкаПоСрокамГодности.Оборудование;
			Движение.ГоденДо = ВыборкаПоСрокамГодности.ГоденДо;
			Движение.ЭксплуатироватьДо = ДобавитьМесяц(Дата,ВыборкаПоСрокамГодности.ОборудованиеСрокЭксплуатации);
			Движение.Количество = Списываем;
			Движение.Сумма = СуммаСписания;
			
			ОсталосьСписать = ОсталосьСписать - Списываем;
			
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры
