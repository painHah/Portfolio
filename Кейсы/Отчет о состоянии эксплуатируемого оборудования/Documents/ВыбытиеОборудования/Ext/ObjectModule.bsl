
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Дата = НачалоМесяца(Дата);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВыбытиеОборудования.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ВыбытиеОборудования КАК ВыбытиеОборудования
	|ГДЕ
	|	ВыбытиеОборудования.Дата = &Дата
	|	И ВыбытиеОборудования.Ссылка <> &Ссылка
	|	И ВыбытиеОборудования.Проведен";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;	
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон("На указанный месяц уже введен документ: %1", Выборка.Ссылка);
	Сообщение.Сообщить();
	Отказ = Истина;	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОборудованиеОрганизации.Записывать = Истина;
	Запрос = Новый Запрос; 
	Запрос.Текст = "ВЫБРАТЬ 
	|	&Дата КАК Период,
	|	ОборудованиеОрганизацииОстатки.СостояниеОборудования КАК СостояниеОборудования,
	|	ОборудованиеОрганизацииОстатки.Оборудование КАК Оборудование,
	|	ОборудованиеОрганизацииОстатки.ГоденДо КАК ГоденДо,
	|	ОборудованиеОрганизацииОстатки.ЭксплуатироватьДо КАК ЭксплуатироватьДо,
	|	ОборудованиеОрганизацииОстатки.КоличествоОстаток КАК Количество,
	|	ОборудованиеОрганизацииОстатки.СуммаОстаток КАК Сумма
	|ИЗ
	|	РегистрНакопления.ОборудованиеОрганизации.Остатки(
	|			&Дата,
	|			ГоденДо < &Дата
	|				ИЛИ (ЭксплуатироватьДо > ДАТАВРЕМЯ(1, 1, 1)
	|				И ЭксплуатироватьДо < &Дата)) КАК ОборудованиеОрганизацииОстатки";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОборудованиеОрганизации.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;	
	КонецЦикла;	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Дата = НачалоМесяца(ТекущаяДатаСеанса());
КонецПроцедуры




