
&НаКлиенте
Процедура ПоказыватьПомеченныхНаУдаление(Команда)
	ПоказыватьПомеченныхНаУдаление = Не ПоказыватьПомеченныхНаУдаление;
	УстановитьОтборПомеченныхНаУдаление();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьОтборПомеченныхНаУдаление();
КонецПроцедуры


&НаКлиенте
Процедура УстановитьОтборПомеченныхНаУдаление()
	НастройкаСписка = Список.КомпоновщикНастроек.Настройки.Отбор;
	ПолеОтбора = Новый ПолеКомпоновкиДанных ("ПометкаУдаления");
	ТипПоляОтбора = Тип("ЭлементОтбораКомпоновкиДанных");
	
	ОтборПоПометкеУдаления = Неопределено;
	Для Каждого ЭлементОтбора Из НастройкаСписка.Элементы Цикл
		Если ЭлементОтбора.ЛевоеЗначение = ПолеОтбора Тогда
			ОтборПоПометкеУдаления = ЭлементОтбора;
			Прервать;
		КонецЕсли;
	КонецЦикла; 
	
	Если ОтборПоПометкеУдаления = Неопределено Тогда
		ОтборПоПометкеУдаления = НастройкаСписка.Элементы.Добавить(ТипПоляОтбора);
		ОтборПоПометкеУдаления.ЛевоеЗначение = ПолеОтбора;
		ОтборПоПометкеУдаления.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборПоПометкеУдаления.ПравоеЗначение = Ложь;
		ОтборПоПометкеУдаления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли;
	
	ОтборПоПометкеУдаления.Использование = Не ПоказыватьПомеченныхНаУдаление;
	
КонецПроцедуры
