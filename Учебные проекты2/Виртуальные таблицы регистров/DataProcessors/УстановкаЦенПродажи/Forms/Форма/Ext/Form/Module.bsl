
&НаСервере
Процедура УстановитьНаСервере(ВсяНоменклатураКОбработке)
	Для Каждого Номенклатура Из ВсяНоменклатураКОбработке Цикл
		МененджерЗаписи = РегистрыСведений.ЦеныПродажиНоменклатуры.СоздатьМенеджерЗаписи();
		МененджерЗаписи.Номенклатура = Номенклатура;
		МененджерЗаписи.Период = ТекущаяДата();
		МененджерЗаписи.Цена = ФиксированнаяЦена;
		МененджерЗаписи.Записать();
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Установить(Команда)
	УстановитьЦенуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВариантУстановкиПриИзменении(Элемент)
	Элементы.ФиксированнаяЦена.Доступность = (ВариантУстановки = 0);
	Элементы.ПроцентОтТекущейЦены.Доступность = (ВариантУстановки = 2);
КонецПроцедуры


&НаСервере
Процедура УстановитьЦенуНаСервере()
	
	ВсяНоменклатураКОбработке = НоменклатураКОбработке();
	Если ВариантУстановки = 0 Тогда
		УстановитьНаСервере(ВсяНоменклатураКОбработке);
	ИначеЕсли ВариантУстановки = 1 Тогда
		УдалитьЦеныНоменклатуры(ВсяНоменклатураКОбработке);
	ИначеЕсли ВариантУстановки = 2 Тогда
		УстановитьЦеныНоменклатурыПроцентом(ВсяНоменклатураКОбработке);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЦеныНоменклатуры(ВсяНоменклатураКОбработке)
	
	Для Каждого Номенклатура Из ВсяНоменклатураКОбработке Цикл
		
		НаборЗаписей = РегистрыСведений.ЦеныПродажиНоменклатуры.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Номенклатура.Установить(Номенклатура);
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Элементы.ФиксированнаяЦена.Доступность = (ВариантУстановки = 0);
	Элементы.ПроцентОтТекущейЦены.Доступность = (ВариантУстановки = 2);
КонецПроцедуры   


&НаСервере
Процедура УстановитьЦеныНоменклатурыПроцентом(ВсяНоменклатураКОбработке)
	
	Для Каждого Номенклатура Из ВсяНоменклатураКОбработке Цикл
		
		ПоследняяЦена = ПолучитьПоследнююЦенуНоменклатуры(Номенклатура);
		Если ПоследняяЦена = 0 Тогда
			Продолжить;
		КонецЕсли;
		НоваяЦена = ПоследняяЦена * (1 + ПроцентОтТекущейЦены / 100); 
		
		УстановитьНовуюЦенуНоменклатуры(Номенклатура, НоваяЦена);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПоследнююЦенуНоменклатуры(Номенклатура)

	Отбор = Новый Структура;
	Отбор.Вставить("Номенклатура", Номенклатура);
	ПоследняяЦена = РегистрыСведений.ЦеныПродажиНоменклатуры.ПолучитьПоследнее(, Отбор);
	
	Возврат ПоследняяЦена.Цена;
	
КонецФункции

&НаСервере
Процедура УстановитьНовуюЦенуНоменклатуры(Номенклатура, Цена)
	
	МенеджерЗаписи = РегистрыСведений.ЦеныПродажиНоменклатуры.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Номенклатура = Номенклатура;
	МенеджерЗаписи.Период = ТекущаяДата();
	МенеджерЗаписи.Цена = Цена;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры  

&НаСервере
Функция НоменклатураКОбработке()
	
	ВсяНоменклатураКОбработке = Новый Массив;
	Для Каждого СтрокаСписка Из СписокНоменклатуры Цикл
		
		НоменклатураВСтроке = СтрокаСписка.Номенклатура;
		Если НоменклатураВСтроке.ЭтоГруппа Тогда
			ДополнитьМассивЭлементамиВложеннымиВГруппу(НоменклатураВСтроке, ВсяНоменклатураКОбработке);
		Иначе
			ВсяНоменклатураКОбработке.Добавить(НоменклатураВСтроке);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВсяНоменклатураКОбработке;
	
КонецФункции

Процедура ДополнитьМассивЭлементамиВложеннымиВГруппу(ГруппаНоменклатуры, ВсяНоменклатураКОбработке)
	
	Выборка = Справочники.Номенклатура.Выбрать(ГруппаНоменклатуры); 
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЭтоГруппа Тогда 
			ДополнитьМассивЭлементамиВложеннымиВГруппу(Выборка.Ссылка, ВсяНоменклатураКОбработке); 
		Иначе
			ВсяНоменклатураКОбработке.Добавить(Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.ГруппыИЭлементы);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыФормы, Элементы.СписокНоменклатуры);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОтборСтрок = Новый Структура;
	ОтборСтрок.Вставить("Номенклатура", ВыбранноеЗначение);
	
	СтрокиТаблицы = СписокНоменклатуры.НайтиСтроки(ОтборСтрок);
	Если СтрокиТаблицы.Количество () > 0 Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = СписокНоменклатуры.Добавить();
	НоваяСтрока.Номенклатура = ВыбранноеЗначение;
	
КонецПроцедуры
